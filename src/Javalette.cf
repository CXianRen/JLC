-- programs ------------------------------------------------

entrypoints Prog ;

Program.   Prog ::= [TopDef] ;

TypeDef.   TopDef ::= "typedef" "struct" Ident "*" Ident ";" ;

StructDef. TopDef ::= "struct" Ident "{" [SStmt] "}" ";" ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Blk ;
FnDefS.    TopDef ::= Ident Ident "(" [Arg] ")" Blk ;

separator nonempty TopDef "" ;

Argument.  Arg ::= Type Ident;
Ptrgument. Arg ::= Ident Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Block.     Blk ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Blk ;

Decl.      Stmt ::= Type [Item] ";" ;

DeclStruct. Stmt ::= Ident [Item] ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

AssArr.    Stmt ::= Expr "=" Expr  ";" ;
-- @todo: it can merger with Ass, if have enough time. 

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForLoop.   Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;
ForStruct. Stmt ::= "for" "(" Ident Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

MInner.    SStmt ::= Type Ident  ";" ;
MArray.    SStmt ::= Type [BracketsOpt] Ident ";" ;
Mpoint.    SStmt ::= Ident Ident ";" ;
separator  SStmt "" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Doub.      Type ::= "double" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

ArrayType. Type ::= Type [BracketsOpt] ;
SArrayType. Type ::= Ident [BracketsOpt] ;

BracketsEmpty.   BracketsOpt ::= "[]" ;

separator nonempty BracketsOpt "";

-- Expressions ---------------------------------------------
ENewArray. Expr6 ::= "new" Type [DimExpr] ;
ENewSArray. Expr6 ::= "new" Ident [DimExpr];

Dim.       DimExpr ::= "[" Expr "]" ;

terminator nonempty DimExpr "" ;

EDot.      Expr6 ::= Expr6 "." Ident ;

EAcc.      Expr7 ::= Expr7 [DimExpr];


-- struct
ENewStruct. Expr6 ::= "new" Ident;
EMember.   Expr7 ::= Expr6 MAccOp Ident; 
TypeCast.  Expr6 ::= "(" Ident ")" "null";

ELitInt.   Expr7 ::= Integer ;

ELitDoub.  Expr7 ::= Double ;

ELitTrue.  Expr7 ::= "true" ;

ELitFalse. Expr7 ::= "false" ;

EString.   Expr7 ::= String ;

EVar.      Expr7 ::= Ident ;

EApp.      Expr7 ::= Ident "(" [Expr] ")" ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 7 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

MAcc.      MAccOp ::= "->" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

